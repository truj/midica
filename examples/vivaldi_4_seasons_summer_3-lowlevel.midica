// This example file provides the 3rd movement of "Four Seasons Summer" by Antonio Vivaldi.
// It comprises the measures 197 until the end (measure 326).
// 
// This file assumes standard program configuration:
// - Note System:       International: c, d, e, ... (lower case)
// - Half Tone Symbols: # / b (c#, cb, eb, db, ...)
// - Octave Naming:     +n/-n
// - Syntax:            Mixed Case Syntax
// 
// Conventions used in this example file:
//
// - Function naming:
//   Functions related to a certain measure start with 'c',
//   followed by the channel number where the function is called first,
//   followed by 'm', followed by the start measure number,
//   followed by '_', followed by the end measure number.
// - Channel commands
//   The channel number in channel commands is always expressed
//   by the variable $c.

META
	copyright  (c) Mozilla Public License, v. 2.0, author: Jan Trukenm√ºller
	title      The Four Seasons (Summer) - 3rd movement
	composer   Antonio Vivaldi
END

// init channels with instruments
INSTRUMENTS
	0   VIOLIN         Lead Violin
	1   VIOLIN         Violin I
	2   VIOLIN         Violin II
	3   VIOLA          Viola
	4   CELLO          Violoncello
	5   CONTRABASS     Double Bass
	6   HARPSICHORD    Organ or Harpsichord (Right Hand)
	7   HARPSICHORD    Organ or Harpsichord (Left Hand)
	10  VIOLIN         Lead Violin (2nd Voice)
	14  CELLO          Violoncello (2nd Voice)
END

// define constants
CONST $p = v=49    // piano
CONST $f = v=96    // forte

FUNCTION rest_full_measure
	$c  -  /2.
END

// set key signature, time signature and tempo
*  tempo  150
*  key    g/min
*  time   3/4

////////////////////////////////////
// channel 0: Lead Violin
////////////////////////////////////

VAR $c = 0  // set current channel

// measure 1-5
FUNCTION c0m1_5
	$c  g   /16        $f
	$c  g-  /16  q=11
	$c  f   /16
	$c  g-  /16  q=11
	$c  eb  /16
	$c  g-  /16  q=11
	$c  d   /16
	$c  g-  /16  q=7
	$c  g-  /8
	$c  -   /8
	CALL  rest_full_measure
END
CALL  c0m1_5

// m. 6-9
FUNCTION c0m6_9
	$c  d+  /16         $f
	$c  d   /16  q=11
	$c  c+  /16
	$c  d   /16  q=11
	$c  bb  /16
	$c  d   /16  q=11
	$c  a   /16
	$c  d   /16  q=7
	$c  d   /8
	$c  -   /8
END
CALL  c0m6_9

// m. 10
FUNCTION c0m10
	$c  -    /16
	$c  d+   /16
	$c  c+   /16
	$c  bb   /16
	$c  a    /16
	$c  g    /16
	$c  f#   /16
	$c  e    /16
	$c  d    /16
	$c  c    /16
	$c  bb-  /16
	$c  a-   /16
END
CALL  c0m10

// m. 11-19
FUNCTION c0m16  // only definition yet
	$c  g    /16
	$c  d+2  /16
	$c  c+2  /16
	$c  bb+  /16
	$c  g+   /16
	$c  f#+  /16
	$c  e+   /16
	$c  d+   /16
	$c  c+   /16
	$c  bb   /16
	$c  a    /16
	$c  g    /16
END
FUNCTION c0m17  // only definition yet
	$c  g    /16  q=12
END
FUNCTION c0m11_18
	// m. 11
	$c  g-   /16  q=12
	
	// m. 12
	$c  g-   /16
	$c  g+   /16
	$c  f+   /16
	$c  eb+  /16
	$c  d+   /16
	$c  c+   /16
	$c  bb   /16
	$c  a    /16
	$c  g    /16
	$c  f    /16
	$c  eb   /16
	$c  d    /16
	
	// m. 13-15
	$c  c    /16  q=13
	$c  bb+  /16
	$c  g+   /16
	$c  f+   /16
	$c  eb+  /16
	$c  d+   /16
	$c  c+   /16
	$c  bb   /16
	$c  a    /16
	$c  g    /16
	$c  f    /16
	$c  eb   /16  q=13
	
	// m. 16-18
	CALL  c0m16  // m. 16
	CALL  c0m17  // m. 17
	CALL  c0m16  // m. 18
END
CALL  c0m11_18   // m. 11-18
CALL  c0m17      // m. 19

// m. 20
FUNCTION c0m20
	{ q=3
		$c  d+  /16
		$c  g   /16  q=3
	}
END
CALL  c0m20

// m. 21-28
FUNCTION c0m22_23  // only definition yet
	// m. 22
	$c  -    /16
	$c  a    /16
	$c  bb   /16
	$c  c+   /16
	$c  d+   /16
	$c  e+   /16
	$c  f#+  /16
	$c  g+   /16
	$c  a+   /8
	$c  -    /8
	
	// m. 23
	$c  -    /16
	$c  bb   /16
	$c  c+   /16
	$c  d+   /16
	$c  eb+  /16
	$c  f#+  /16
	$c  g+   /16
	$c  a+   /16
	$c  bb+  /8
	$c  -    /8
END
FUNCTION c0m24  // only definition yet
	$c  -    /16
	$c  d+   /16
	$c  e+   /16
	$c  f#+  /16
	$c  g+   /16
	$c  a+   /16
	$c  bb+  /16
	$c  c+2  /16
	$c  d+2  /8
	$c  -    /8
END
FUNCTION c0m21_28
	// m. 21
	$c  d    /16
	$c  a    /16
	$c  bb   /16
	$c  c+   /16
	$c  d+   /16
	$c  e+   /16
	$c  f#+  /16
	$c  g+   /16
	$c  a+   /8
	$c  -    /8
	
	// m. 22-25
	CALL  c0m22_23
	CALL  c0m24     q=2
	
	// m. 26-28
	CALL  c0m22_23
	CALL  c0m24
END
CALL  c0m21_28

// m. 29-37
FUNCTION c0m29_37
	// m. 29
	{ q=3
		$c  bb+  /16
		$c  g+   /16
		$c  d+   /16
		$c  bb   /16
	}
	
	// m. 30
	{ q=3
		$c  d+   /16
		$c  bb   /16
		$c  g    /16
		$c  d    /16
	}
	
	// m. 31
	{ q=3
		$c  g    /16
		$c  d    /16
		$c  bb-  /16
		$c  g-   /16
	}
	
	// m. 32
	{ q=6
		$c  g    /16
		$c  b    /16
	}
	
	// m. 33
	{ q=6
		$c  g    /16
		$c  c+   /16
	}
	
	// m. 34
	{ q=6
		$c  g    /16
		$c  c#+  /16
	}
	
	// m. 35-36
	{ q=12
		$c  a    /16
		$c  d+   /16
	}
	
	// m. 37
	{ q=6
		$c  a    /16
		$c  c#+  /16
	}
END
CALL  c0m29_37

// m. 38-40/1
$c  d+  /16           // m. 38/1
FUNCTION c0m38second_40first
	// m. 38
	$c  d    /16
	$c  e    /16
	$c  f    /16
	$c  g    /16
	$c  a    /16
	$c  b    /16
	$c  c#+  /16
	$c  d+   /16
	$c  a    /16
	$c  b    /16
	$c  c#+  /16
	
	// m. 39
	{ q=2
		$c  d+   /16
		$c  e+   /16
		$c  f+   /16
		$c  g+   /16
		$c  a+   /16
	}
	$c  b+    /16
	$c  c#+2  /16
	
	// m. 40/1
	$c  d+2   /4
END
CALL  c0m38second_40first

// m. 40/2
$c  -     /16
$c  a+    /16  q=2
$c  c#+2  /16  q=2
$c  e+2   /16  q=2
$c  a+2   /16

// m. 41
FUNCTION c0m41part
	$c  a+    /16
	$c  d     /16
	$c  a+    /16
END
$c  a+2   /16
CALL  c0m41part
$c  g+2   /16
CALL  c0m41part
$c  f+2   /16
CALL  c0m41part

// m. 42
$c  e+2   /16
CALL  c0m41part
$c  f+2   /16
CALL  c0m41part
$c  g+2   /16
CALL  c0m41part

// m. 43
$c  a+2   /16
CALL  c0m41part
$c  g+2   /16
CALL  c0m41part
$c  f+2   /16
CALL  c0m41part

// m. 44
$c  d/a+/e+2  /4
$c  -         /16
$c  d+        /16  q=2
$c  f+        /16  q=2
$c  a+        /16  q=2
$c  d+2       /16

// m. 45
FUNCTION c0m45part
	$c  d+    /16
	$c  d     /16
	$c  d+    /16
END
$c  d+2       /16
CALL  c0m45part
$c  c+2       /16
CALL  c0m45part
$c  bb+       /16
CALL  c0m45part

// m. 46
$c  a+    /16
CALL  c0m45part
$c  bb+   /16
CALL  c0m45part
$c  c#+2   /16
CALL  c0m45part

// m. 47
$c  d+2   /16
CALL  c0m45part
$c  c+2   /16
CALL  c0m45part
$c  bb+   /16
CALL  c0m45part

// m. 48-50
$c  d/d+/a+  /4
$c  -        /16
$c  a+2      /16  q=2
$c  g+2      /16  q=2
$c  f+2      /16  q=2
$c  eb+2     /16  q=2
$c  d+2      /16  q=2
$c  c+2      /16  q=2
$c  bb+      /16  q=2
$c  a+       /16  q=2
$c  g+       /16  q=2
$c  f+       /16  q=2
$c  e+       /16  q=2
$c  d+       /16  q=2
$c  c+       /16  q=2
$c  bb       /16  q=2
$c  a        /16  q=2
$c  g#       /16

// m. 51-54
*  0,10           // synchronize with helper channel 10
10  d    *2.      // put second voice to helper channel

// 1st voice, m. 51
{ q=3
	$c  a    /8
	$c  g#   /8
}

// 1st voice, m. 52
$c  g    /8
$c  eb   /8
$c  f    /8
$c  d    /8
$c  f    /8
$c  c#   /8

// 1st voice, m. 53-54
{ q=6
	$c  d    /8
	$c  c#   /8
}

// m. 55/1
$c  d    /8

// m. 55/2-63
FUNCTION c0m55second_63
	// m. 55/2
	$c  d    /8  q=2
	$c  d+   /8
	$c  f+   /8
	$c  a+   /8
	
	// m. 56-57
	$c  bb+  /8
	$c  d    /8  q=2
	$c  d+   /8
	$c  g+   /8
	$c  bb+  /8  q=2
	$c  e    /8  q=2
	$c  e+   /8
	$c  g+   /8
	$c  bb+  /8
	
	// m. 58-59
	$c  a+   /8
	$c  f    /8  q=2
	$c  c+   /8
	$c  f+   /8
	$c  a+   /8  q=2
	$c  d    /8  q=2
	$c  d+   /8
	$c  f+   /8
	$c  a+   /8
	
	// m. 60-62
	$c  g+   /8
	$c  d    /8  q=2
	$c  bb   /8
	$c  eb+  /8
	$c  d+   /8  q=2
	$c  c    /8  q=2
	$c  c+   /8
	$c  eb+  /8
	$c  g+   /8  q=2
	$c  d    /8  q=2
	$c  b    /8
	$c  d+   /8
	$c  g+   /8
	
	// m. 63
	$c  f+   /8
	$c  d    /8  q=2
	$c  b    /8
	$c  d+   /8
	$c  f+   /8
END
CALL  c0m55second_63

// m. 64-66
FUNCTION c0m64_66
	// m. 64
	$c  eb+  /16
	$c  eb   /16
	$c  f    /16
	$c  g    /16
	$c  ab   /16
	$c  b    /16
	$c  c+   /16
	$c  d+   /16
	$c  eb+  /8
	$c  -    /8
	
	// m. 65-66
	{ q=2
		$c  -    /16
		$c  eb   /16
		$c  f    /16
		$c  g    /16
		$c  ab   /16
		$c  b    /16
		$c  c+   /16
		$c  d+   /16
		$c  eb+  /8
		$c  -    /8
	}
END
CALL  c0m64_66

// m. 67-69
FUNCTION c0m67_69
	// m. 67
	{ q=3
		$c  eb   /16
		$c  c+   /16
		$c  bb   /16
		$c  c+   /16
	}
	
	// m. 68
	{ q=3
		$c  f    /16
		$c  c+   /16
		$c  bb   /16
		$c  c+   /16
	}
	
	// m. 69
	{ q=3
		$c  g    /16
		$c  c+   /16
		$c  b    /16
		$c  c+   /16
	}
END
CALL  c0m67_69

// m. 70/1
FUNCTION c0m70first
	$c  c+  /16
END
CALL  c0m70first

// m. 70/2-73
FUNCTION c0m70second_73
	// m. 70/2
	$c  c   /16  q=11
		
	// m. 71
	$c  bb  /16
	$c  c   /16  q=11
	
	// m. 72
	$c  ab  /16
	$c  c   /16  q=11
	
	// m. 73
	$c  g   /16
	$c  c   /16  q=7
	$c  c   /8
	$c  -   /8
END
CALL  c0m70second_73

// m. 74
$c  g+   /2.  m
{ q=6
	$c  eb+  /16
	$c  d+   /16
}

// m. 75
$c  g+   /2.  m
{ q=6
	$c  d+  /16
	$c  b   /16
}

// m. 76
$c  g+   /2.  m
{ q=6
	$c  eb+  /16
	$c  c+   /16
}

// m. 77
$c  f+   /2.  m
{ q=6
	$c  d+  /16
	$c  bb  /16
}

// m. 78
$c  g+   /2.  m
{ q=6
	$c  eb+ /16
	$c  bb  /16
}

// m. 79
$c  eb+   /16
$c  g+    /16  q=2
$c  ab+   /16  q=2
$c  bb+   /16  q=2
$c  c+2   /16  q=2
$c  d+2   /16  q=2
$c  eb+2  /16

// m. 79
{ q=3
	$c  eb+2  /16
	$c  bb+   /16
	$c  g+    /16
	$c  eb+   /16
}

// m. 81
$c  f+    /16
$c  ab+   /16  q=2
$c  bb+   /16  q=2
$c  c+2   /16  q=2
$c  d+2   /16  q=2
$c  eb+2  /16  q=2
$c  f+2   /16

// m. 82
{ q=3
	$c  f+2   /16
	$c  c+2   /16
	$c  a+    /16
	$c  f+    /16
}

// m. 83
$c  g+    /16
$c  bb+   /16  q=2
$c  c+2   /16  q=2
$c  d+2   /16  q=2
$c  eb+2  /16  q=2
$c  f+2   /16  q=2
$c  g+2   /16

// m. 84
{ q=3
	$c  g+2   /16
	$c  d+2   /16
	$c  b+    /16
	$c  g+    /16
}

// m. 85
$c  g+2  /16
$c  c    /16  q=11

// m. 86-96
FUNCTION c0m86_96
	// m. 86
	{ q=3
		$c  ab  /16
		$c  c   /16  q=3
	}
	
	// m. 87
	$c  b-  /16  q=12
	
	// m. 88
	{ q=3
		$c  g    /16
		$c  bb-  /16  q=3
	}
	
	// m. 89
	$c  a-  /16  q=12
	
	// m. 90
	{ q=3
		$c  f    /16
		$c  ab-  /16  q=3
	}
	
	// m. 91
	$c  g-  /16
	$c  g   /16  q=11
	
	// m. 92
	{ q=3
		$c  eb+  /16
		$c  g    /16  q=3
	}
	
	// m. 93
	$c  f#  /16  q=12

	// m. 94
	{ q=3
		$c  d+  /16
		$c  f   /16  q=3
	}
	
	// m. 95
	$c  e  /16  q=12
	
	// m. 96
	{ q=3
		$c  c+  /16
		$c  eb  /16  q=3
	}
END
CALL  c0m86_96

// m. 97
$c  d   /16  q=4
$c  f#  /16
$c  d   /16  q=3
$c  a   /16
$c  d   /16  q=3

// m. 98
$c  bb  /16
$c  d   /16  q=3
$c  c+  /16
$c  d   /16  q=3
$c  d+  /16
$c  d   /16  q=3

// m. 99
$c  eb+  /16
$c  d    /16  q=3
$c  f#+  /16
$c  d    /16  q=3
$c  g+   /16
$c  d    /16  q=3

// m. 100
$c  a+   /16
$c  d    /16  q=3
$c  bb+  /16
$c  d    /16  q=3
$c  c+2  /16
$c  d    /16  q=3

// m. 101/1
$c  d+2  /16

// m. 101/2-107
FUNCTION c0m101second_107
	// m. 101/2
	$c  d+   /16
	$c  c+   /16
	$c  bb   /16
	$c  a    /16
	$c  g    /16
	$c  f    /16
	$c  e    /16
	$c  d    /16
	$c  c    /16
	$c  bb-  /16
	$c  a-   /16
	
	// m. 102
	$c  g-  /16  q=12
	
	// m. 103
	$c  g-   /16
	$c  g+   /16
	$c  f+   /16
	$c  eb+  /16
	$c  d+   /16
	$c  c+   /16
	$c  bb   /16
	$c  a   /16
	$c  g   /16
	$c  f   /16
	$c  eb  /16
	$c  d   /16
	
	// m. 104
	$c  c  /16  q=12
	
	// m. 105-106
	$c  c    /16
	$c  bb+  /16
	$c  g+   /16
	$c  f+   /16
	$c  eb+  /16
	$c  d+   /16
	$c  c+   /16
	$c  bb   /16
	$c  a    /16
	$c  g    /16
	$c  f    /16
	$c  eb   /16  q=13
	
	// m. 107-108
	$c  g    /16
	$c  d+2  /16
	$c  c+2  /16
	$c  bb+  /16
	$c  g+   /16
	$c  f#+  /16
	$c  e+   /16
	$c  d+   /16
	$c  c+   /16
	$c  bb   /16
	$c  a    /16
	$c  g    /16
END
CALL  c0m101second_107

// m. 108
FUNCTION c0m108
	$c  g    /16  q=12
END
CALL  c0m108

// m. 109
FUNCTION c0m109part
	$c  g    /16
	$c  g-   /16
	$c  g    /16
END
$c  d+   /16
CALL  c0m109part
$c  eb+  /16
CALL  c0m109part
$c  f#+  /16
CALL  c0m109part

// m. 110
FUNCTION c0m110
	$c  g+   /16
	CALL  c0m109part
	$c  f+   /16
	CALL  c0m109part
	$c  eb+  /16
	CALL  c0m109part
END
CALL  c0m110

// m. 111
$c  d+   /16
CALL  c0m109part
$c  eb+  /16
CALL  c0m109part
$c  f#+  /16
CALL  c0m109part

// m. 112
CALL  c0m110

// m. 113-114
$c  g/d+  /16  m   // second voice (m. 113)
FUNCTION c0m113_114
	// m. 113
	$c  g-   /16
	$c  g    /16
	$c  a    /16
	$c  bb   /16
	$c  c+   /16
	$c  d+   /16
	$c  e+   /16
	$c  f#+  /16
	$c  g+   /16
	$c  a+   /16
	$c  bb+  /16
	$c  a+   /16
	
	// m. 114
	$c  g+   /16
	$c  f+   /16
	$c  eb+  /16
	$c  d+   /16
	$c  c+   /16
	$c  bb   /16
	$c  a    /16
	$c  g    /16
	$c  f#   /16
	$c  e    /16
	$c  d    /16
	$c  c    /16
END
CALL  c0m113_114

// m. 115
FUNCTION c0m115
	{ q=6
		$c  bb-  /16  $f
		$c  g-   /16
	}
END
CALL  c0m115

// m. 116-119
{ q=2
	// 116 == 118
	$c  eb   /16
	$c  d    /16
	$c  f#   /16
	$c  e    /16
	$c  g    /16
	$c  f#   /16
	$c  a    /16
	$c  g    /16
	$c  bb   /16
	$c  a    /16
	$c  c+   /16
	$c  bb   /16
	
	// 117 == 119
	$c  d+   /16
	$c  c+   /16
	$c  eb+  /16
	$c  d+   /16
	$c  f+   /16
	$c  e+   /16
	$c  g+   /16
	$c  f#+  /16
	$c  a+   /16
	$c  g+   /16
	$c  bb+  /16
	$c  a+   /16
}

// m. 120-122
FUNCTION c0m121  // only definition yet
	{ q=3
		$c  g    /16
		$c  d    /16
		$c  bb-  /16
		$c  g-   /16
	}
END
FUNCTION c0m120_122
	// m. 120
	$c  d  /16  q=12
	
	// m. 121
	CALL  c0m121
	
	// m. 122
	$c  g  /16  q=12
END
CALL  c0m120_122

// m. 123
FUNCTION c0m123
	$c  f#  /16  q=12
END
CALL  c0m123

// m. 124
CALL  c0m121

// m. 125-130
FUNCTION c0m125_130
	// m. 125
	$c  g  /16  q=12
	
	// m. 126
	$c  f  /16  q=12
	
	// m. 127
	$c  g   /16  q=4
	$c  bb  /16  q=4
	$c  d+  /16  q=4
	
	// m. 128
	$c  g+   /16  q=4
	$c  bb+  /16  q=4
	$c  g+   /16  q=4
	
	// m. 129
	$c  d+  /16  q=4
	$c  bb  /16  q=4
	$c  g   /16  q=4
	
	// m. 130
	$c  g-   /2.
END
CALL  c0m125_130

////////////////////////////////////
// channel 1: Violin I
////////////////////////////////////

VAR $c = 1

// m. 1-40/1
CALL  c0m1_5
CALL  c0m6_9
CALL  c0m10
CALL  c0m11_18
CALL  c0m17          // m. 19
CALL  c0m20
CALL  c0m21_28
CALL  c0m29_37
$c  d+  /16             // m. 38/1
CALL  c0m38second_40first

// m. 40/2-54
FUNCTION c1m40second_54
	// 40/2
	$c  -  /2
	
	// 41-48 (41-44 == 45-48)
	{ q=2
		$c  d  /4   q=10,  $p
		$c  -  /2
	}
	
	// 49-54
	CALL  rest_full_measure  q=6
END
CALL  c1m40second_54

// m. 55-84
$c  a+  /8  $f
CALL  c0m55second_63
CALL  c0m64_66
CALL  c0m67_69
CALL  c0m70first
CALL  c0m70second_73
CALL  rest_full_measure  q=11

// m. 85
FUNCTION c1m85
	$c  c  /16  q=12
END
CALL  c1m85

// m. 86-96
CALL  c0m86_96

// m. 97-100
FUNCTION c1m97_100
	// m. 97
	$c  d  /4
	$c  -  /2
	
	// m. 98-100
	CALL  rest_full_measure  q=3
END
CALL  c1m97_100

// m. 101-108
$c  -  /16
CALL  c0m101second_107
CALL  c0m108

// m. 109-112
FUNCTION c1m109_112
	$c  g   /4
	$c  g-  /4  q=11, $p
END
CALL  c1m109_112

// m. 113-115
CALL  c0m113_114
CALL  c0m115

// m. 116-130
CALL  c1m97_100   // m. 116-119
CALL  c0m120_122
CALL  c0m123
CALL  c0m121      // m. 124
CALL  c0m125_130

////////////////////////////////////
// channel 2: Violin II
////////////////////////////////////

VAR $c = 2

// m. 1-10
CALL  c0m1_5
CALL  c0m6_9
CALL  rest_full_measure

// m. 11-19  -  ch2(11-19) == ch0(10-18)
CALL  c0m10     // m. 11
CALL  c0m11_18  // m. 12-19

// m. 20
FUNCTION c2m20
	$c  g   /16  q=4
	{ q=2
		$c  d+  /16
		$c  g   /16  q=3
	}
END
CALL  c2m20

// m. 21
$c  d    /16
$c  f#   /16
$c  g    /16
$c  a    /16
$c  bb   /16
$c  c+   /16
$c  d+   /16
$c  e+   /16
$c  f#+  /8
$c  -    /8

// m. 22
FUNCTION c2m22
	$c  -    /16
	$c  f#   /16
	$c  g    /16
	$c  a    /16
	$c  bb   /16
	$c  c+   /16
	$c  d+   /16
	$c  e+   /16
	$c  f#+  /8
	$c  -    /8
END
CALL  c2m22

// m. 23
FUNCTION c2m23
	$c  -    /16
	$c  g    /16
	$c  a    /16
	$c  bb   /16
	$c  c+   /16
	$c  d+   /16
	$c  eb+  /16
	$c  f#+  /16
	$c  g+   /8
	$c  -    /8
END
CALL  c2m23

// m. 24-28
CALL  c2m22
CALL  c2m22  q=2
CALL  c2m23
CALL  c2m22

// m. 29
{ q=3
	$c  bb   /16
	$c  d+   /16
	$c  g+   /16
	$c  bb+  /16
}

// m. 30
{ q=3
	$c  d   /16
	$c  g   /16
	$c  bb  /16
	$c  d+  /16
}

// m. 31
{ q=3
	$c  g-   /16
	$c  bb-  /16
	$c  d    /16
	$c  g    /16
}

// m. 32
{ q=6
	$c  d  /16
	$c  g  /16
}

// m. 33
{ q=4
	$c  eb  /16
	$c  g   /16
}
{ q=2
	$c  eb  /16
	$c  c   /16
}

// m. 34
{ q=6
	$c  e  /16
	$c  g  /16
}

// m. 35
{ q=4
	$c  f  /16
	$c  a  /16
}
{ q=2
	$c  d  /16
	$c  a  /16
}

// m. 36-37
{ q=12
	$c  e  /16
	$c  a  /16
}

// m. 38-40/1
$c  f  /16                    // m. 38/1
CALL  c0m38second_40first

// m. 41
CALL  c1m40second_54

// m. 55
$c  f+  /8       $f
$c  a-  /8  q=2
$c  a   /8
$c  d+  /8
$c  f+  /8

// m. 56
$c  g+   /8
$c  bb-  /8  q=2
$c  bb   /8
$c  d+   /8
$c  g+   /8

// m. 57
$c  g+  /8
$c  c   /8  q=2
$c  c+  /8
$c  e+  /8
$c  g+  /8

// m. 58
$c  f+  /8
$c  c   /8  q=2
$c  a   /8
$c  c+  /8
$c  f+  /8

// m. 59
$c  f+   /8
$c  bb-  /8  q=2
$c  bb   /8
$c  d+   /8
$c  f+   /8

// m. 60
$c  eb+  /8
$c  bb-  /8  q=2
$c  g    /8
$c  bb   /8
$c  eb+  /8

// m. 61
$c  eb+  /8
$c  ab-  /8  q=2
$c  ab   /8
$c  c+   /8
$c  eb+  /8

// m. 62
$c  d+   /8
$c  b-   /8  q=2
$c  g    /8
$c  b    /8
$c  d+   /8

// m. 63
$c  d+  /8
$c  b-  /8  q=2
$c  g   /8
$c  b   /8
$c  d+  /8

// m. 64
$c  c+  /16
$c  c   /16
$c  d   /16
$c  eb  /16
$c  f   /16
$c  g   /16
$c  a   /16
$c  b   /16
$c  c+  /8
$c  -   /8

// m. 65-66
{ q=2
	$c  -   /16
	$c  c   /16
	$c  d   /16
	$c  eb  /16
	$c  f   /16
	$c  g   /16
	$c  a   /16
	$c  b   /16
	$c  c+  /8
	$c  -   /8
}

// m. 67-101
CALL  c0m67_69
CALL  c0m70first
CALL  c0m70second_73
CALL  rest_full_measure  q=11
CALL  c1m85
CALL  c0m86_96
CALL  c1m97_100
CALL  rest_full_measure  // m. 101

// m. 102-108  -  ch2(102-108) == ch0(101-107)
$c  -  /16                 // m. 102/1
CALL  c0m101second_107  // m. 102/2-108

// m. 109-126
CALL  c1m109_112
CALL  c0m113_114
CALL  c0m115
CALL  c1m97_100   // m. 116-119
CALL  c0m120_122
$c  a-  /16  q=12    // m. 123
CALL  c0m121      // m. 124
$c  a-  /16  q=24    // m. 125-126

// m. 127
$c  bb-  /16
$c  d    /16  q=3
$c  g    /16  q=4
$c  bb   /16  q=4

// m. 128
$c  d+   /16  q=4
$c  g+   /16  q=4
$c  bb+  /16  q=4

// m. 129
$c  g+  /16  q=4
$c  d+  /16  q=4
$c  bb  /16  q=4

// m. 130
$c  g-   /2.

////////////////////////////////////
// channel 3: Viola
////////////////////////////////////

VAR $c = 3

// m. 1-10
CALL  c0m1_5
CALL  c0m6_9
CALL  rest_full_measure

// m. 11-20
$c  g-  /16  q=24  // m. 11-12
$c  c   /16  q=24  // m. 13-14
$c  eb  /16  q=24  // m. 15-16
$c  g   /16  q=36  // m. 17-19
CALL  c2m20

// m. 21-28
FUNCTION c3m21second  // only definition yet
	$c  -   /4+/16
	$c  d+  /16
	$c  a   /16
	$c  f#  /16
END
FUNCTION c3m23  // only definition yet
	$c  g   /4
	$c  -   /4+/16
	$c  d+  /16
	$c  bb  /16
	$c  g   /16
END
$c  f#  /4         // m. 21/1
CALL  c3m21second
$c  d   /4         // m. 22/1
CALL  c3m21second  // m. 22/2
CALL  c3m23        // m. 23
$c  f#  /4         // m. 24/1
CALL  c3m21second  // m. 24/2
$c  d   /4         // m. 25/1
CALL  c3m21second  // m. 25/2
$c  d   /4         // m. 26/1
CALL  c3m21second  // m. 26/2
CALL  c3m23        // m. 27
$c  f#  /4         // m. 28/1
CALL  c3m21second  // m. 28/2

// m. 29-31
$c  d  /8  q=18

// m. 32
{ q=6
	$c  b-  /16
	$c  d   /16
}

// m. 33
{ q=6
	$c  c   /16
	$c  eb  /16
}

// m. 34
FUNCTION  c3m34part
	$c  c#  /16
	$c  e   /16
END
CALL  c3m34part  q=6

// m. 35
{ q=6
	$c  d  /16
	$c  f  /16
}

// m. 36
{ q=6
	$c  d  /16
	$c  e  /16
}

// m. 37
CALL  c3m34part  q=6

// m. 38-54
$c  d  /4
$c  -  /2
CALL  rest_full_measure // m. 39
$c  f  /4                  // m. 40/1
CALL  c1m40second_54

// m. 55
$c  d  /4  $f
$c  -  /8
$c  a  /8
$c  f  /16
$c  g  /16
$c  f  /16
$c  e  /16

// m. 56
$c  d   /4
$c  -   /8
$c  d+  /8
$c  bb  /16
$c  a   /16
$c  g   /16
$c  f   /16

// m. 57
$c  e    /4
$c  -    /8
$c  g    /8
$c  e    /16
$c  d    /16
$c  c    /16
$c  bb-  /16

// m. 58
$c  d   /4
$c  -   /8
$c  c+  /8
$c  a   /16
$c  g   /16
$c  f   /16
$c  eb  /16

// m. 59
$c  d    /4
$c  -    /8
$c  f    /8
$c  d    /16
$c  c    /16
$c  bb-  /16
$c  a-   /16

// m. 60
$c  bb-  /4
$c  -    /8
$c  bb   /8
$c  g    /16
$c  f    /16
$c  eb   /16
$c  d    /16

// m. 61
$c  c    /4
$c  -    /8
$c  eb   /8
$c  c    /16
$c  bb-  /16
$c  ab-  /16
$c  g-   /16

// m. 62
$c  d   /4
$c  -   /8
$c  f   /8
$c  d   /16
$c  c   /16
$c  b-  /16
$c  a-  /16

// m. 63
$c  b-  /4
$c  -   /8
$c  d   /8
$c  b-  /16
$c  a-  /16
$c  g-  /16
$c  f-  /16

// m. 64-66
{ q=3
	$c  g-  /4
	$c  -   /4
	$c  c+  /16
	$c  g   /16
	$c  eb  /16
	$c  c   /16
}

// m. 67-73
$c  g-   /8   q=6   // m. 67
$c  ab-  /8   q=4   // m. 68/1
$c  ab   /8   q=2   // m. 68/2
$c  d    /8   q=6   // m. 69
$c  eb   /16        // m. 70/1
$c  c    /16  q=11  // m. 70/2
$c  bb   /16        // m. 71/1
$c  c    /16  q=11  // m. 71/2
$c  ab   /16        // m. 72/1
$c  c    /16  q=11  // m. 72/2
$c  g    /16        // m. 73/1
$c  c    /16  q=7   // m. 73/2
$c  c    /8         // m. 73/3
$c  -    /8         // m. 73/4

// m. 74-84
CALL  rest_full_measure  q=11

// m. 85-101
CALL  c1m85
CALL  c0m86_96
CALL  c1m97_100
CALL  rest_full_measure  // m. 101

// m. 102-108
$c  g-  /16  q=24  // m. 102-103
$c  c   /16  q=24  // m. 104-105
$c  eb  /16  q=24  // m. 106-107
$c  g   /16  q=12  // m. 108

// m. 109-119
FUNCTION c3m109_119
	CALL  c1m109_112
	$c  g-  /4                  // m. 113/1
	$c  -   /2                  // m. 113/2
	CALL  rest_full_measure  // m. 114
	CALL  c0m115
	CALL  c1m97_100          // m. 116-119
END
CALL  c3m109_119

// m. 120
$c  d   /4
$c  -   /16
$c  d+  /16
$c  c+  /16
$c  bb  /16
$c  a   /16
$c  g   /16
$c  f#  /16
$c  e   /16

// m. 121-126
$c  d    /16  q=12  // m. 121
$c  eb   /16  q=12  // m. 122
$c  d    /16  q=12  // m. 123
$c  bb-  /16  q=4   // m. 124/1
$c  d    /16  q=8   // m. 124/2
$c  eb   /16  q=12  // m. 125
$c  d    /16  q=12  // m. 126

// m. 127
$c  d    /16
$c  bb-  /16  q=3
$c  d    /16  q=4
$c  g    /16  q=4

// m. 128
$c  bb  /16  q=4
$c  d+  /16  q=4
$c  d   /16  q=4

// m. 129
$c  bb-  /16  q=4
$c  g-   /16  q=4
$c  d    /16  q=4

// m. 130
$c  g-   /2.

////////////////////////////////////
// channel 4: Violoncello
////////////////////////////////////

VAR $c = 4

// m. 1-10
CALL  c0m1_5  s=-12
CALL  c0m6_9  s=-12
CALL  rest_full_measure

// m. 11-19
FUNCTION  c4m11_19
	$c  g-2  /8  q=12  // m. 11-12
	$c  c-   /8  q=12  // m. 13-14
	$c  eb-  /8  q=12  // m. 15-16
	$c  g-   /8  q=18  // m. 17-19
END
CALL  c4m11_19

// m. 20
CALL  c2m20  s=-12

// m. 21-31
FUNCTION c4m21_31
	{ q=2
		
		// m. 21-22 == 25-26
		{ q=12
			$c  d-  /16
			$c  d   /16
		}

		// m. 23 == 27
		{ q=6
			$c  d-   /16
			$c  bb-  /16
		}
		
		// m. 24 == 28
		{ q=6
			$c  d-   /16
			$c  a-   /16
		}
	}
	
	// m. 29-31
	$c  g-2  /8  q=18
END
CALL  c4m21_31

// m. 32-54
FUNCTION c4m32_54
	{ q=6                             // m. 32
		$c  g-2  /16
		$c  g-   /16
	}
	{ q=6                             // m. 33
		$c  c-  /16
		$c  c   /16
	}
	{ q=6                             // m. 34
		$c  a-2  /16
		$c  a-   /16
	}
	{ q=6                             // m. 35
		$c  d-  /16
		$c  d   /16
	}
	{ q=12                            // m. 36-37
		$c  a-2  /16
		$c  a-   /16
	}
	{ q=2                             // m. 38-39 == 40-41
		$c  d-  /4
		$c  -   /2
		CALL  rest_full_measure
	}
	CALL  rest_full_measure  q=11  // m. 42-52
	{ q=6                             // m. 53-54
		$c  d-   /8
		$c  a-2  /8
	}
END
CALL  c4m32_54

// m. 55-64
FUNCTION c4m56  // only definition yet
	$c  g-2   /16
	$c  g-    /16
	$c  f-    /16
	$c  eb-   /16
	$c  d-    /16
	$c  c-    /16
	$c  bb-2  /16
	$c  a-2   /16
	$c  g-2   /8
	$c  -     /8
END
FUNCTION c4m55_64
	// m. 55
	$c  d-   /16
	$c  d    /16
	$c  c    /16
	$c  bb-  /16
	$c  a-   /16
	$c  g-   /16
	$c  f-   /16
	$c  e-   /16
	$c  d-   /8
	$c  -    /8
	
	// m. 56
	CALL  c4m56
	
	// m. 57
	$c  c-   /16
	$c  c    /16
	$c  bb-  /16
	$c  a-   /16
	$c  g-   /16
	$c  f-   /16
	$c  e-   /16
	$c  d-   /16
	$c  c-   /8
	$c  -    /8
	
	// m. 58
	$c  f-2  /16
	$c  f-   /16
	$c  e-   /16
	$c  d-   /16
	$c  c-   /16
	$c  bb-2 /16
	$c  a-2  /16
	$c  g-2  /16
	$c  f-2  /8
	$c  -    /8
	
	// m. 59
	$c  bb-2  /16
	$c  bb-   /16
	$c  a-    /16
	$c  g-    /16
	$c  f-    /16
	$c  eb-   /16
	$c  d-    /16
	$c  c-    /16
	$c  bb-2  /8
	$c  -     /8
	
	// m. 60
	$c  eb-2  /16
	$c  eb-   /16
	$c  d-    /16
	$c  c-    /16
	$c  bb-2  /16
	$c  a-2   /16
	$c  g-2   /16
	$c  f-2   /16
	$c  eb-2  /8
	$c  -     /8
	
	// m. 61
	$c  ab-2  /16
	$c  ab-   /16
	$c  g-    /16
	$c  f-    /16
	$c  eb-   /16
	$c  d-    /16
	$c  c-    /16
	$c  bb-2  /16
	$c  ab-2  /8
	$c  -     /8
	
	// m. 62
	$c  b-2  /16
	$c  b-   /16
	$c  a-   /16
	$c  g-   /16
	$c  f-   /16
	$c  eb-  /16
	$c  d-   /16
	$c  c-   /16
	$c  b-2  /8
	$c  -    /8
	
	// m. 63
	CALL  c4m56
	
	// m. 64
	$c  c-   /16
	$c  c    /16
	$c  bb-  /16
	$c  ab-  /16
	$c  g-   /16
	$c  f-   /16
	$c  eb-  /16
	$c  d-   /16
	$c  c-   /8
	$c  -    /8
END
CALL  c4m55_64

// m. 65-69
FUNCTION c4m65_69
	{ q=2             // m. 65-66
		$c  -    /16
		$c  c    /16
		$c  bb-  /16
		$c  ab-  /16
		$c  g-   /16
		$c  f-   /16
		$c  eb-  /16
		$c  d-   /16
		$c  c-   /8
		$c  -    /8
	}
	$c  eb-  /8  q=6  // m. 67
	$c  f-   /8  q=6  // m. 68
	$c  g-   /8  q=4  // m. 69/1
	$c  g-2  /8  q=2  // m. 69/2
END
CALL  c4m65_69

// m. 70-73
$c  c-   /16
CALL  c0m70second_73  s=-12

// m. 74-84
FUNCTION c4m74  // only definition yet
	$c  c-  /8
	$c  c   /16
	$c  b-  /16
	$c  c   /8
	$c  c-  /8   q=2
	$c  c   /8
END
FUNCTION c4m74_84
	CALL  c4m74
	$c  g-2   /4        // m. 75/1
	$c  -     /2        // m. 75/2
	CALL  c4m74      // m. 76
	$c  bb-2  /4        // m. 77/1
	$c  -     /2        // m. 77/2
	
	// m. 78-79
	$c  eb-2  /8
	$c  eb-   /16
	$c  d-    /16
	$c  eb-   /8
	$c  eb-2  /8   q=2
	$c  eb-   /8
	$c  g-2   /4        // m. 79/1
	$c  -     /2        // m. 79/2
	
	// m. 80-81
	$c  g-2   /8
	$c  g-    /16
	$c  f-    /16
	$c  g-    /8
	$c  g-2   /8   q=2
	$c  g-    /8
	$c  ab-2  /4        // m. 81/1
	$c  -     /2        // m. 81/2
	
	// m. 82-83
	$c  a-2   /8
	$c  a-    /16
	$c  g-    /16
	$c  a-    /8
	$c  a-2   /8   q=2
	$c  a-    /8
	$c  bb-2  /4        // m. 83/1
	$c  -     /2        // m. 83/2
	
	// m. 84
	$c  b-2   /8
	$c  b-    /16
	$c  a-    /16
	$c  b-    /8
	$c  b-2   /8   q=2
	$c  b-    /8
END
CALL c4m74_84

// m. 85-96
CALL  c1m85     s=-12
CALL  c0m86_96  s=-12

// m. 97-101
*  4,14       // synchronize with helper channel 14
14  d-  /4    // put second voice to helper channel
FUNCTION c4m97_101
	$c  d-  /1.     // m. 97-98
	$c  d-  /1.+/4  // m. 99-101/1
	$c  -   /2      // m. 101/2
END
CALL  c4m97_101

// m. 102-108
FUNCTION  c4m102_108
	$c  g-2  /8  q=12  // m. 102-103
	$c  c-   /8  q=12  // m. 104-105
	$c  eb-  /8  q=12  // m. 106-107
	$c  g-   /8  q=6   // m. 108
END
CALL  c4m102_108

// m. 109-119
CALL  c3m109_119  s=-12

// m. 120
FUNCTION c4m120
	$c  d-   /16
	$c  d    /16
	$c  c    /16
	$c  bb-  /16
	$c  a-   /16
	$c  bb-  /16
	$c  a-   /16
	$c  g-   /16
	$c  f#-  /16
	$c  e-   /16
	$c  d-   /16
	$c  c-   /16
END
CALL  c4m120

// m. 121-126
FUNCTION c4m121_126
	$c  bb-2  /16  q=12  // m. 121
	$c  c-    /16  q=12  // m. 122
	$c  d-    /16  q=12  // m. 123
	$c  bb-2  /16  q=4   // m. 124/1
	$c  d-    /16  q=8   // m. 124/2
	$c  eb-   /16  q=12  // m. 125
	$c  d-    /16  q=12  // m. 126
END
CALL  c4m121_126

// m. 127-130
FUNCTION c4m127_130
	{ q=18               // m. 127-129
		$c  g-2  /16
		$c  g-   /16
	}
	$c  g-2  /2.         // m. 130
END
CALL  c4m127_130

////////////////////////////////////
// channel 5: Double Bass
////////////////////////////////////

VAR $c = 5

// m. 1-10
$c  g-2   /8     $f      // m. 1
$c  -     /2+/8
$c  f-2   /8             // m. 2
$c  -     /2+/8
$c  eb-2  /8             // m. 3
$c  -     /2+/8
$c  d-2   /8             // m. 4
$c  -     /2+/8
CALL  rest_full_measure  // m. 5
$c  d-    /8             // m. 6
$c  -     /2+/8
$c  c-    /8             // m. 7
$c  -     /2+/8
$c  bb-2  /8             // m. 8
$c  -     /2+/8
$c  a-2   /8             // m. 9
$c  -     /2+/8
CALL  rest_full_measure  // m. 10

// m. 11-19
CALL  c4m11_19  s=-12

// m. 20-28
FUNCTION c5m21  // only definition yet
	$c  d-2  /8
	$c  -    /2+/8
END
$c  g-2  /8                 // m. 20
$c  -    /8
$c  d-   /8
$c  -    /4.
CALL  c5m21  q=4         // m. 21-24
CALL  rest_full_measure  // m. 25
CALL  c5m21  q=3         // m. 26-28

// m. 29-38
$c  g-3  /8     q=19  // m. 29-32/1
$c  -    /2+/8        // m. 32/2
$c  c-2  /8           // m. 33/1
$c  -    /2+/8        // m. 33/2
$c  a-3  /8           // m. 34/1
$c  -    /2+/8        // m. 34/2
$c  d-2  /8           // m. 35/1
$c  -    /2+/8        // m. 35/2
{ q=2                 // m. 36 == 37
	$c  a-3  /8
	$c  -    /2+/8
}
$c  d-2  /8           // m. 38/1
$c  -    /2+/8        // m. 38/2

// m. 39-54
CALL  rest_full_measure  q=16  

// m. 55-64
FUNCTION  c5m55
	$c  d-2   /8     $f
	$c  -     /4.
	$c  d-2   /8
	$c  -     /8
END
CALL  c5m55        // m. 55
CALL  c5m55  s=-5  // m. 56
CALL  c5m55  s=-2  // m. 57
CALL  c5m55  s=-9  // m. 58
CALL  c5m55  s=-4  // m. 59
CALL  c5m55  s=+1  // m. 60
CALL  c5m55  s=-6  // m. 61
CALL  c5m55  s=-3  // m. 62
CALL  c5m55  s=-7  // m. 63
CALL  c5m55  s=-2  // m. 64

// m. 65-69
CALL  c4m65_69  s=-12

// m. 70-73
FUNCTION  c5m70
	$c  c-2   /8
	$c  -     /2+/8
END
CALL  c5m70         // m. 70: c-2
CALL  c5m70  s=+10  // m. 71: bb-2
CALL  c5m70  s=+8   // m. 72: ab-2
CALL  c5m70  s=+7   // m. 73: g-2

// m. 74-84
CALL  rest_full_measure  q=11

// m. 85-96
CALL  c5m70         // m. 85: c-2
CALL  c5m70  s=+8   // m. 86: ab-2
CALL  c5m70  s=-1   // m. 87: b-3
CALL  c5m70  s=+7   // m. 88: g-2
CALL  c5m70  s=-3   // m. 89: a-3
CALL  c5m70  s=+5   // m. 90: f-2
CALL  c5m70  s=-5   // m. 91: g-3
CALL  c5m70  s=+15  // m. 92: eb-
CALL  c5m70  s=+6   // m. 93: f#-2
CALL  c5m70  s=+14  // m. 94: d-
CALL  c5m70  s=+4   // m. 95: e-2
CALL  c5m70  s=+12  // m. 96: c-

// m. 97-114
CALL  c1m97_100  s=-24
CALL  rest_full_measure       // m. 101
CALL  c4m102_108 s=-12
$c  g-2  /4                      // m. 109/1
$c  -    /2                      // m. 109/2
CALL  rest_full_measure  q=5  // m. 110-114

// m. 115-116
{ q=3              // m. 115
	$c  bb-3  /8
	$c  -     /8
}
$c  d-2  /4        // m. 116
$c  -    /2

// m. 117-120
CALL  rest_full_measure  q=4

// m. 121-126
CALL  c4m121_126  s=-12

// 127-129
{ q=3
	$c  g-3  /8
	$c  -    /2+/8
}

// m. 130
$c  g-3  /2.

////////////////////////////////////
// channel 6: Organ or Harpsichord (Right Hand)
////////////////////////////////////

VAR $c = 6

// m. 1-10
CALL  c0m1_5
CALL  c0m6_9
CALL  rest_full_measure

// m. 11-20
$c  g-/d     /4  q=6  // m. 11-12
$c  g-/eb    /4  q=6  // m. 13-14
$c  g-/c/g   /4  q=6  // m. 14-16
$c  bb-/d    /4  q=3  // m. 17
$c  bb-/d/g  /4  q=3  // m. 18
$c  d/g/bb   /4  q=3  // m. 19
CALL  c2m20

// m. 21-28
{ q=2
	$c  f#/a/d+  /4  q=6  // m. 21-22 == 25-26
	$c  d/g/bb   /4  q=3  // m. 23 == 27
	$c  d/f#/a   /4  q=3  // m. 24 == 28
}

// m. 29-37
$c  bb-/d/g  /2.  q=3  // m. 29-31
$c  d/g/b    /2.       // m. 32
$c  eb/g/c+  /2.       // m. 33
$c  e/a/c#+  /2.       // m. 34
$c  f/a/d+   /2.       // m. 35
$c  e/a/d+   /2.       // m. 36
$c  e/a/c#+  /2.       // m. 37

// m. 38-41
{ q=2
	$c  f/a/d+  /4
	$c  -       /2
	CALL  rest_full_measure
}

// m. 42-52
CALL  rest_full_measure  q=11

// m. 53-54
{ q=2
	$c  a-  /2.  m
	{ q=3
		$c  f-  /8
		$c  c-  /8
	}
}

// m. 55-66
$c  f/a      /2+/8  m  // m. 55
$c  -        /2
$c  d+       /8
$c  -        /8
$c  d/g/bb   /2+/8     // m. 56
$c  -        /8
$c  e/g/c+   /2+/8     // m. 57
$c  -        /8
$c  c/f/a    /2+/8     // m. 58
$c  -        /8
$c  d/f/a    /2+/8     // m. 59
$c  -        /8
$c  bb-/eb/g /2+/8     // m. 60
$c  -        /8
$c  c/f/ab   /2+/8     // m. 61
$c  -        /8
$c  d/g/d+   /2+/8     // m. 62
$c  -        /8
$c  d/f/b    /2+/8     // m. 63
$c  -        /8
{ q=3                  // m. 64-66
	$c  eb/g/c+  /2+/8
	$c  -        /8
}

// m. 67-70/1
$c  g/c+     /2.  // m. 67
$c  d/ab/c+  /2.  // m. 68
$c  d/g/c+   /2   // m. 69/1
$c  d/g/b    /4   // m. 69/2
$c  eb/g/c+  /16  // m. 70/1

// m. 70/2-73
CALL  c0m70second_73

// m. 74-96
$c  g/c+/eb+    /4  q=3  // m. 74
$c  g/b/d+      /4       // m. 75
$c  -           /2
$c  g/c+/eb+    /4  q=3  // m. 76
$c  bb/d+/f+    /4       // m. 77
$c  -           /2
$c  bb/eb+/g+   /4  q=3  // m. 78
$c  bb/eb+/bb+  /4       // m. 79
$c  -           /2
$c  eb/bb       /2.      // m. 80
$c  c/eb/ab     /4       // m. 81
$c  -           /2
$c  f/c+        /2.      // m. 82
$c  d/bb        /4       // m. 83
$c  -           /2
$c  g/d+        /2.      // m. 84
$c  eb/g/c+     /2.      // m. 85
$c  eb/ab       /2.      // m. 86
$c  d/f/ab      /2.      // m. 87
$c  d/g         /2.      // m. 88
$c  c/eb/g      /2.      // m. 89
$c  c/f         /2.      // m. 90
$c  b-/d/f      /2.      // m. 91
$c  c           /4       // m. 92
$c  g           /4
$c  c+          /4
$c  a/c+/eb+    /2.      // m. 93
$c  a/d+        /2.      // m. 94
$c  g/bb/d+     /2.      // m. 95
$c  g/c+        /2.      // m. 96

// m. 97-101
CALL  rest_full_measure  q=5

// m. 102-130
$c  g-     /2.  q=2              // m. 102-103
$c  g-/eb  /2.                   // m. 104
$c  c      /2.                   // m. 105
$c  bb-/g  /2.                   // m. 106
$c  c/a    /2.                   // m. 107
$c  d/bb   /2.                   // m. 108

{ q=2                            // m. 109-112
	$c  bb-/d  /4                // m. 109 == 111
	$c  c/eb   /4  $p
	$c  d/f#   /4
	$c  eb/g   /4                // m. 110 == 112
	$c  d/f    /4
	$c  c/eb   /4
}
$c  bb-/d    /4                  // m. 113
$c  -        /2
CALL  rest_full_measure       // m. 114
$c  d/g/d+   /2.   $f            // m. 115
$c  d/f#/a   /4                  // m. 116
$c  -        /2
CALL  rest_full_measure  q=4  // m. 117-120
$c  d/g      /2.                 // m. 121
$c  eb/g/a   /2.                 // m. 122
$c  d/f#/a   /2.                 // m. 123
$c  d/g/bb   /4                  // m. 124
$c  d/g      /2
$c  d/g/a    /2.                 // m. 125
$c  d/f#/a   /2.                 // m. 126
$c  d/g/bb   /2.                 // m. 127
$c  d/bb/d+  /2.                 // m. 128
$c  d/g/bb   /4                  // m. 129
$c  g        /4
$c  d        /4
$c  g-       /2.                 // m. 130

////////////////////////////////////
// channel 7: Organ or Harpsichord (Left Hand)
////////////////////////////////////

VAR $c = 7

// m. 1-
CALL  c0m1_5    s=-12
CALL  c0m6_9    s=-12
CALL  rest_full_measure
CALL  c4m11_19
CALL  c2m20     s=-12
CALL  c4m21_31
CALL  c4m32_54
CALL  c4m55_64
CALL  c4m65_69
$c  c-   /16                    // m. 70/1
CALL  c0m70second_73  s=-12
CALL  c4m74_84
CALL  c1m85     s=-12
CALL  c0m86_96  s=-12
CALL  c4m97_101
CALL  c4m102_108
CALL  c3m109_119  s=-12
CALL  c4m120
CALL  c4m121_126
CALL  c4m127_130

