// This example file provides the traditional song
// "London Bridge is Falling Down".
// 
// This file assumes standard program configuration:
// - Note System:       International: c, d, e, ... (lower case)
// - Half Tone Symbols: # / b (c#, cb, d#, db, ...)
// - Octave Naming:     +n/-n
// - Syntax:            Mixed Case Syntax

META
	copyright  (c) Mozilla Public License, v. 2.0, author: Jan Trukenm√ºller
	title      London Bridge is Falling Down
	composer   traditional
	lyrics     traditional
END

INSTRUMENTS
	0  LEAD_SAWTOOTH   Vocals
	1  E_GUITAR_CLEAN  Guitar
END

// global settings
*  tempo  150
*  time   4/4
*  key    d/maj

// initialize channel defaults
0: (d=75%)  (v=100)
1: (d=100%) (v=70)

// execute the verses with different lyrics
CALL verse(Lon, don_, Bridge_, is_, fal, ling_, down)
CALL verse(Build_, it_, up_, with_, wood_, and_, clay)
CALL verse(Wood_, and_, clay_, will_, wash_, a, way)
CALL verse(Build_, it_, up_, with_, bricks_, and_, mortar)
CALL verse(Bricks_, and_, mor, tar_, will_, not_, stay)
CALL verse(Build_, it_, up_, with_, iron_, and_, steel)
CALL verse(Iron_, and_, steel_, will_, bend_, and_, bow)
CALL verse(Build_, it_, up_, with_, silver_, and_, gold)
CALL verse(Silver_, and_, gold_, will_be_, stolen_, a, way)
CALL verse(Set_, a_, man_, to_, watch_, all_, night)
CALL verse(Suppose_, the_, man_, should_, fall_, a, sleep)
CALL verse(Give_him_, a_, pipe_, to_, smoke_, all_, night)

FUNCTION verse
	CALL vocals($[0], $[1], $[2], $[3], $[4], $[5], $[6])
	CALL guitar
END

///////////////////////////
// vocals
///////////////////////////

FUNCTION vocals
	CALL vocals_m1($[0], $[1], $[2], $[3])  // London bridge is
	CALL vocals_m2($[4], $[5], $[6]\c\r)    // falling down,
	CALL vocals_m3($[4], $[5], $[6]\c_)     // falling down,
	CALL vocals_m2($[4], $[5], $[6].\r)     // falling down.
	CALL vocals_m1($[0], $[1], $[2], $[3])  // London bridge is
	CALL vocals_m2($[4], $[5], $[6].\r)     // falling down,
	CALL vocals_m7_8()                      // My fair lady.
END
FUNCTION vocals_m1
	//    Lon-             don          bridge         is
	0: | (l=$[0]) a:4.  (l=$[1]) b:8   (l=$[2]) a:4   (l=$[3]) g |
END
FUNCTION vocals_m2
	//    fal-            ling          down
	0: | (l=$[0]) f#:4   (l=$[1]) g   (l=$[2]) a:2 |
END
FUNCTION vocals_m3
	//    fal-           ling           down
	0: | (l=$[0]) e:4   (l=$[1]) f#   (l=$[2]) g:2 |
END
FUNCTION vocals_m7_8
	//    My fair Lady
	0: | (l=My_) e:2   (l=fair_) a | (l=La) f#:4   (l=dy.\n) d:2. |
END

///////////////////////////
// guitar
///////////////////////////

// define some guitar chords
CHORD  amaj  a-2 / e- / a- / c#
CHORD  dmaj  d-  / a- / d  / f#

// define a picking pattern for both chords
PATTERN pick
	: | 0:8 1 2 1 3 1 2 3 |
END

FUNCTION guitar
	// use the same pattern with both chords
	1: | (q=2) dmaj:pick | amaj:pick |
	1: | (q=3) dmaj:pick | amaj:pick | dmaj:pick |
END
